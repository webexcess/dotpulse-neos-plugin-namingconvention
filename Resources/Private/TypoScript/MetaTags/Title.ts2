# Seite
# - titleOverride
# - titleInherit
# - title

# Pfad
# - showPathTitle

# Seiten im Pfad
# - showInPathTitle
# - useOverrideInPath

# Variabeln
# - %site%
# - %title%

prototype(Dotpulse.NamingConvention:Collection) < prototype(Neos.Fusion:Collection) {

	collection = ${q(documentNode).get()}

	itemName = 'node'
	iterationName = 'iterator'

	// Implode node titles with the divider
	itemRenderer = Neos.Fusion:Array {
		page = Neos.Fusion:Case {
			hasOverrideAndLast {
				condition = ${q(node).property('titleOverride') && iterator.isLast}
				renderer =  ${q(node).property('titleOverride')}
			}
			hasOverrideAndUseInPath {
				condition = ${q(node).property('titleOverride') && q(node).property('useOverrideInPath')}
				renderer =  ${q(node).property('titleOverride')}
			}
			hasInherit {
				condition = ${!String.isBlank(q(node).parents('[titleInherit]').property('titleInherit'))}
				renderer = Neos.Fusion:Case {
					isLastOrIsNotTheSame {
						condition = ${iterator.isLast || q(node).parents('[titleInherit]').get(0) != q(node).children().filter('[instanceof TYPO3.Neos:Document]').parents('[titleInherit]').get(0)}
						renderer =  ${String.replace(String.replace(q(node).parents('[titleInherit]').first().property('titleInherit'), '%title%', q(node).property('title')), '%site%', site.context.currentSite.name)}
					}
					default {
						condition = ${true}
						renderer =  ${q(node).property('title')}
					}
				}
			}
			default {
				condition = ${true}
				renderer =  ${String.replace(q(node).property('title'), '&shy;', '')}
			}
		}
		pageDivider = ${iterator.isLast ? '' : settings.dividerPage}
	}
	@process.siteName = ${(settings.positionSite == 'start' ? site.context.currentSite.name + (value ? settings.dividerSite : '' ) : '' ) + String.stripTags(value) + (settings.positionSite == 'end' ? (value ? settings.dividerSite : '') + site.context.currentSite.name : '')}
}

prototype(TYPO3.Neos:Page).head.titleTag {
	// Read settings
	@context.settings = ${Configuration.setting('Dotpulse.NamingConvention.Title')}
	title = Neos.Fusion:Case {
		titleOverride {
			condition = ${(node == site) && q(node).property('titleOverride') ? true : false}
			renderer = ${q(node).property('titleOverride')}
		}
		isRoot {
			condition = ${(node == site) && settings.positionSite == false ? true : false}
			renderer = ${site.context.currentSite.name}
		}
		showPath {
			condition =${settings.showPath}
			renderer = Neos.Fusion:Case {
				titleAsc {
					condition = ${settings.showPath == 'asc'}
					renderer = Dotpulse.NamingConvention:Collection {
						// If "showPathTitle" is set, the rootline will returned, pages with "showInPathTitle=false" will be filtered
						collection = ${q(documentNode).property('showPathTitle') ? q(documentNode).add(q(documentNode).parents('[instanceof TYPO3.Neos:Document][showInPathTitle=true]')).slice(0, -1).get() : q(documentNode).get()}
					}
				}
				titleDesc {
					condition = true
					renderer = Dotpulse.NamingConvention:Collection {
						collection = ${Array.reverse(q(documentNode).property('showPathTitle') ? q(documentNode).add(q(documentNode).parents('[instanceof TYPO3.Neos:Document][showInPathTitle=true]')).slice(0, -1).get() : q(documentNode).get())}
					}
				}
			}
		}
		titleOnly {
			condition = true
			renderer = Dotpulse.NamingConvention:Collection
		}
	}
	// Put everything together
	renderContent = ${this.title}

	content = ${String.replace(String.pregReplace(String.pregReplace(this.renderContent, '/\n/' , ' '),'/ +/' , ' '), 'Â ', ' ')}
}
